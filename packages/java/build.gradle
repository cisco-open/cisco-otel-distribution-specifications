plugins {
    id 'maven-publish'
    id 'java'
}

group = 'com.epsagon'
version = '0.0.22-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.opentelemetry:opentelemetry-api:1.12.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'specifications'
            version = version

            from components.java
        }
    }
    repositories {
        maven {
            name = 'specifications'
            url = uri("https://oss.sonatype.org/content/repositories/snapshots")
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

tasks {
    test {
        useJUnitPlatform()
    }

    compileJava {
        options.release.set(8)
    }
}

tasks.register('incrementPatchVersion') {
    doLast {
        String oldPatch = version.substring(version.lastIndexOf('.') + 1, version.indexOf('-'))
        int newPatch = oldPatch.toInteger() + 1
        String major = version.substring(0, version.lastIndexOf('.') + 1)
        String s = buildFile.getText().replaceFirst("version = '$version'", "version = '" + major + newPatch + "-SNAPSHOT'")
        buildFile.setText(s)
    }
}
